@name ODM Gear By Naki
@persist Left Right [LeftPos RightPos Pos]:vector
@trigger none
runOnKeys(owner(),1)
interval(50)

if(first()){
    
    #rope(number index,entity ent1,vector v1,entity ent2,vector v2,number addlength,number width,string mat,number rigid)
    for(I=1,4){
        holoCreate(I)
        holoPos(I,entity():pos())
        holoAng(I,ang(0,0,0))
        holoAlpha(I,0)
    }
    MDL="models/hunter/plates/plate.mdl"
    P1=propSpawn(MDL,ang(0,0,0),1)
    P2=propSpawn(MDL,ang(0,0,0),1)
    P3=propSpawn(MDL,ang(0,0,0),1)
    P4=propSpawn(MDL,ang(0,0,0),1)
    Array=array(P1,P2,P3,P4)
    
    foreach(I,E:entity=Array){
        E:setNoCollideAll(1)
        E:setAlpha(0)
    }
    
    rope(1,P1,vec(0,0,0),P3,vec(0,0,0),0,2,"cable/cable",1)
    rope(2,P2,vec(0,0,0),P4,vec(0,0,0),0,2,"cable/cable",1)
    
    P1:parentTo(holoEntity(1))
    P2:parentTo(holoEntity(2))
    P3:parentTo(holoEntity(3))
    P4:parentTo(holoEntity(4))
    
    holoCreate(5)
    holoPos(5,owner():pos())
    holoAlpha(5,0)
    holoParent(5,owner())
    
    vrSetPlayer(owner())
    owner():soundPlay("gas",0,"d1_town.GasJet")
}

if(vrPlayerInVR(owner())){
    LeftButton=vrLeftGrab()
    RightButton=vrRightGrab()
    ReelIn=vrPrimaryFire()
}else{
    LeftButton=owner():keyAttack1()
    RightButton=owner():keyAttack2()
    ReelIn=owner():keySprint()
}

if(owner():weapon():type()=="none"){
if(changed(LeftButton)&LeftButton){
    Left=!Left
}
if(changed(RightButton)&RightButton){
    Right=!Right
}
}
if(ReelIn){
    particleGravity(vec(0))
    particleSetDuration(2)
    particleSetStartSize(15)
    particleSetEndSize(8)
    particleSetStartAlpha(255)
    particleSetEndAlpha(0)
    particleSetPosition(owner():pos()+vec(0,0,45))
    particleSetVelocity(-owner():eye()*1)
    particleSetMaterial("particles/smokey")
    particleCollision(0)
    particleCreate()
}
if(changed(Right)){
    if(Right){
        holoEntity(5):soundPlay("fire",0,"weapons/alyx_gun/alyx_gun_fire5.wav")
        holoEntity(5):soundPlay("hook",0.8,"weapons/tripwire/ropeshoot.wav")
    }else{
        holoEntity(5):soundPlay("unhook",0,"npc/roller/blade_in.wav")
    }
}
if(changed(Left)){
    if(Left){
        holoEntity(5):soundPlay("fire",0,"weapons/alyx_gun/alyx_gun_fire5.wav")
        holoEntity(5):soundPlay("hook",0.8,"weapons/tripwire/ropeshoot.wav")
    }else{
        holoEntity(5):soundPlay("unhook",0,"npc/roller/blade_in.wav")
    }
}
if(Left&!Right){
    Pos=LeftPos
}
if(Left){
    holoPos(3,owner():pos()+vec(0,0,0)+(-owner():right()*25))
    holoEntity(1):parent():applyForce(((owner():pos()-holoEntity(1):parent():pos()):normalized())*4000*(1+(ReelIn*4)))
}else{
    holoPos(3,vec(0,0,0))
}
if(Right){
    holoPos(4,owner():pos()+vec(0,0,0)+(owner():right()*25))
    holoEntity(2):parent():applyForce(((owner():pos()-holoEntity(2):parent():pos()):normalized())*4000*(1+(ReelIn*4)))
}else{
    holoPos(4,vec(0,0,0))
}
if(Right&!Left){
    Pos=RightPos
}
if(Right&Left){
    Pos=(LeftPos+RightPos)/2
}
if(!Right&!Left){
    Pos=vec(0,0,0)
}
soundVolume("gas",1*ReelIn)
owner():plyApplyForce(
((((-(owner():pos()-Pos):normalized())*100*(1+(ReelIn*1.5))))*(Left|Right))+
(((-owner():right()*(owner():keyLeft()-owner():keyRight()))+
(owner():forward()*(owner():keyForward()-owner():keyBack()))+
(owner():up()*(owner():keyJump()-owner():keyDuck())))*150*ReelIn)-
owner():vel()*0.005+(0.028*ReelIn))

LeftPos=holoEntity(1):pos()
RightPos=holoEntity(2):pos()

if(changed(Left)){
    if(Left){
        holoPos(1,owner():aimPos())
        holoParent(1,owner():aimEntity())
    }else{
        holoPos(1,vec(0,0,0))
        holoUnparent(1)
    }
}

if(changed(Right)){
    if(Right){
        holoPos(2,owner():aimPos())
        holoParent(2,owner():aimEntity())
    }else{
        holoPos(2,vec(0,0,0))
        holoUnparent(2)
    }
}
